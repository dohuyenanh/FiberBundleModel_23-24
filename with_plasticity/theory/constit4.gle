size 18 12

!REMEMBER: show gridlines at special points
!read constit4.tex
!Given:
E = 1           !elastic modulus of intact fibers
e_y = 0.35       !mean value of yielding thresholds
e_b = 3.5       !mean value of broken thresholds
de_y = 0.2     !radius of the uniform distribution of e_y
de_b = 1.1     !radius of the uniform distribution of e_b
a = 0.2         !ratio of the elastic modulus of yielding fibers to E

!To make it easier to graph, introduce some markers at the borders between each 2 adjacent intervals:
b1 = e_y - de_y     !start yielding
b2 = e_y + de_y     !stop intact
b3 = e_b - de_b     !start breaking
b4 = e_b + de_b     !every fiber is broken
b5 = 5            !upper limit of strain

amove 0 1
begin graph
    size 18 12
    title "Constitutive Relation of Fibers with Plasticity" hei 0.5 dist 0.35

    data "../simulation/data/constit.txt" d30=c1,c2
    d30 marker circle msize 0.3 color black deresolve 200 ! every 200th point will be plotted

    let d31=E*x from 0 to b1
    d31 line color red lwidth 0.04  

    let d32=E*x*(0.5-(x-e_y)/(2*de_y))+E/(2*de_y)*((1+a)/2*x^2-a*(e_y-de_y)*x-(1-a)/2*b1^2) from b1 to b2
    d32 line color red lwidth 0.04  

    let d33=E*((1-a)*e_y+a*x) from b2 to b3
    d33 line color red lwidth 0.04  

    let d34=E*((1-a)*e_y+a*x)*(1-(x-b3)/(2*de_b)) from b3 to b4
    d34 line color red lwidth 0.04 

    let d35=0*x from b4 to b5
    d35 line color red lwidth 0.04  

    let d10=0*x from 0 to b1
    d10 line color blue lwidth 0.05 y2axis lstyle 3 ! Soft Teal

    let d20=0*x from 0 to b3
    d20 line color green lwidth 0.05 y2axis lstyle 3 ! Lavender

    let d11=(x-b1)/(2*de_y) from b1 to b2
    d11 line color blue lwidth 0.05 y2axis lstyle 3

    let d21=(x-b3)/(2*de_b) from b3 to b4
    d21 line color green lwidth 0.05 y2axis lstyle 3

    let d12=x^0 from b2 to b5
    d12 line color blue lwidth 0.05 y2axis lstyle 3

    let d22=x^0 from b4 to b5
    d22 line color green lwidth 0.05 y2axis lstyle 3

    xlabels hei 0.7 dist 0.3  	
  	ylabels hei 0.7 dist 0.2

    xsticks length 0.2
    ysticks length 0.2

	xtitle "\epsilon" hei 0.8 dist 0.3
  	ytitle "\sigma" hei 0.8 dist 0.3 

    y2title "P(\varepsilon)" hei 0.8

    y2axis max 1.5
    yaxis min 0
    xaxis max b5

end graph

!gsave
!    set lwidth 0.05 
 !   set color red
  !  amove xg(0.0) yg(0.0)
   ! aline xg(0.2) yg(0.2)
!grestore

gsave
    set lwidth 0.05 
    set lstyle 9
    set color gray10 ! x5
    amove xg(b1) yg(0)
    aline xg(b1) yg(0.7)
grestore

gsave
    set lwidth 0.05 
    set lstyle 9
    set color gray10 ! x5
    amove xg(b2) yg(0)
    aline xg(b2) yg(0.7)
grestore

gsave
    set lwidth 0.05 
    set lstyle 9
    set color gray10 ! x5
    amove xg(b3) yg(0)
    aline xg(b3) yg(0.7)
grestore

gsave
    set lwidth 0.05 
    set lstyle 9
    set color gray10 ! x5
    amove xg(b4) yg(0)
    aline xg(b4) yg(0.7)
grestore

begin key
    hei 0.65
    nobox
    compact 
    position tl 
    offset 0.2 0.2
    text "P_b(\varepsilon)" line color blue lwidth 0.05 lstyle 3
    text "P_y(\varepsilon)" line color green lwidth 0.05 lstyle 3
end key

begin key
    hei 0.65
    nobox
    compact 
    position tr 
    offset 0.2 0.2
    text "theory" line color red lwidth 0.04
    text "simulation" marker circle msize 0.3 color black 
end key

